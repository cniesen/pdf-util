plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

group = "com.niesens.pdfutil"
version = "0.1.0"

java {
	sourceCompatibility = '17'
	withJavadocJar()
	withSourcesJar()
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.apache.pdfbox:pdfbox:3.0.2'
	implementation 'org.apache.pdfbox:pdfbox-tools:3.0.2'
	implementation 'commons-io:commons-io:2.16.1'
	testImplementation 'org.testng:testng:7.9.0'
}

tasks.test {
	useTestNG()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.niesens.pdfutil'
			artifactId = 'pdf-util'
			from components.java

			pom {
				name = 'PDF Utility'
				description = 'PDF Compare Utility'
				url = 'https://github.com/cniesen/pdf-util'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'cniesen'
						name = 'Claus Niesen'
					}
					developer {
						id = 'vinsguru'
						name = 'Vinoth Selvaraj'
					}
				}
				scm {
					connection = 'scm:git:git:github.com:cniesen/pdf-util.git'
					developerConnection = 'scm:git:https://github.com/cniesen/pdf-util.git'
					url = 'https://github.com/cniesen/pdf-util'
				}
			}
		}
	}
	repositories {
		maven {
			name = "OSSRH"
			url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = project.ossrhUsername
				password = project.ossrhPassword
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}


javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}